plugins {
    id 'com.android.application'
    id 'kotlin-android'
}


android {
    compileSdk COMPILE_SDK

    defaultConfig {
        applicationId APPLICATION_ID
        minSdk MIN_SDK
        targetSdk TARGET_SDK
        versionCode VERSION_CODE
        versionName VERSION_NAME
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias "chenyangqi"
            keyPassword "123456"
            storeFile file('../app_key.jks')
            storePassword "123456"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BUILD_TIME", "\"${System.currentTimeMillis()}\"")
            //resValue("String", "title", "debug首页标题")
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BUILD_TIME", "\"${System.currentTimeMillis()}\"")
            //resValue("String", "title", "release首页标题")
        }
    }

    // 设置apk划分维度
    // version用来表示APP版本，比如极速版，普通版，免费版付费版等
    // environment用来表示运行环境，比如系统集成测试环境sit，和生产环境prod等
    flavorDimensions "channel", "version", "environment"

    productFlavors {
        oppo {
            dimension "channel"
            manifestPlaceholders = [MTA_CHANNEL_VALUE: "oppo"]
        }

        vivo {
            dimension "channel"
            manifestPlaceholders = [MTA_CHANNEL_VALUE: "vivo"]
        }

        xiaomi {
            dimension "channel"
            manifestPlaceholders = [MTA_CHANNEL_VALUE: "xiaomi"]
        }

        speed {
            dimension "version"
            versionNameSuffix ".speed"
            applicationIdSuffix ".speed"
            //设置混淆文件
            //consumerProguardFiles 'consumer-rules.pro'
            //配置差异化的logo和appName
            manifestPlaceholders = [
                    logo   : "@mipmap/ic_launcher_speed",
                    appName: "GradleDemo极速版"
            ]
            //定义字段在BuildConfig中
            buildConfigField('boolean', "isSpeed", 'true')
        }

        normal {
            dimension "version"
            //配置差异化的logo和appName
            manifestPlaceholders = [
                    logo   : "@mipmap/ic_launcher",
                    appName: "GradleDemo"
            ]
            buildConfigField('boolean', "isSpeed", 'false')
        }

        sit {
            dimension "environment"
            versionNameSuffix ".sit"
            applicationIdSuffix ".sit"
            applicationIdSuffix ".sit"
            buildConfigField('String', 'baseUrl', '"https://sit..."')
            minSdkVersion 26
        }

        prod {
            dimension "environment"
            buildConfigField('String', 'baseUrl', '"https://prod..."')
        }
    }

    //修改release apk的名字
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name == 'release') {
                def fileName = "GradleDemo_" +
                        "${variant.productFlavors[0].name}_" +
                        "${variant.productFlavors[1].name}_" +
                        "${variant.productFlavors[2].name}_" +
                        "${defaultConfig.versionName}" +
                        "_release.apk"
                output.outputFileName = fileName
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JVM_TARGET
    }
}

dependencies {
    implementation MULTIDEX
    implementation CORE_KTX
    implementation APPCOMPAT
    implementation ANDROID_MATERIAL
    implementation CONSTRAINTLAYOUT
    implementation project(path: ':ib_image')
    implementation("com.squareup.okhttp3:okhttp:4.9.0")
    testImplementation TEST_JUNIT
    androidTestImplementation ANDROID_EXT_JUNIT
    androidTestImplementation ANDROID_TEST_ESPRESSO
}

