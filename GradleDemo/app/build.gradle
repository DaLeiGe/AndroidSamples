plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 30

    defaultConfig {
        applicationId "com.chenyangqi.gradle"
        minSdk 19
        targetSdk 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias 'chenyangqi'
            keyPassword '123456'
            storeFile file('../app_key.jks')
            storePassword '123456'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // 设置apk划分维度
    // version用来表示APP版本，比如极速版，普通版，免费版付费版等
    // environment用来表示运行环境，比如系统集成测试环境sit，和生产环境prod等
    flavorDimensions "version", "environment"

    productFlavors {
        speed {
            dimension "version"
            applicationIdSuffix ".speed"
            //设置混淆文件
            //consumerProguardFiles 'consumer-rules.pro'
            //配置差异化的logo和appName
            manifestPlaceholders = [
                    logo   : "@mipmap/ic_launcher_speed",
                    appName: "GradleDemo极速版"
            ]
        }

        normal {
            dimension "version"
            //配置差异化的logo和appName
            manifestPlaceholders = [
                    logo   : "@mipmap/ic_launcher",
                    appName: "GradleDemo"
            ]
        }

        sit {
            dimension "environment"
            applicationIdSuffix ".sit"
        }

        prod {
            dimension "environment"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}